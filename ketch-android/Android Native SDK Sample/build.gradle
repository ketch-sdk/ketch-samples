apply plugin: "com.github.ben-manes.versions"
apply plugin: "org.jlleitschuh.gradle.ktlint"
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

buildscript {
    ext.buildConfig = [
            'minSdk'    : 28,
            'targetSdk' : 32,
            'compileSdk': 32,
    ]
    ext.versions = [
            'buildTools'   : '33.0.2',
            'ketch'        : '0.3.+',
            'kotlin'       : '1.7.10',
            'ktx'          : '1.8.0',
            'lint'         : '27.2.0',
            'appcompat'    : '1.5.1',
            'material'     : '1.7.0',
            'dokka'        : '1.7.20',
            'junit'        : '5.7.1'
    ]
    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.kotlin:kotlin-serialization:${versions.kotlin}"
        classpath "de.mannodermaus.gradle.plugins:android-junit5:1.7.1.1"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.42.0"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:10.3.0"
        classpath("org.jfrog.buildinfo:build-info-extractor-gradle:4.29.0") {
            exclude group: 'org.codehaus.groovy'
        }
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${versions.dokka}"
    }
}

allprojects {
    repositories {
        google()
        mavenLocal()
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven { url "https://dl.cloudsmith.io/public/indooratlas/mvn-public/maven" }
        maven { url "https://software.mobile.pendo.io/artifactory/android-release" }
        maven {
            url "https://ketch.jfrog.io/artifactory/android/"
            credentials {
                username = System.getenv('ARTIFACTORY_USERNAME')
                password = System.getenv('ARTIFACTORY_PASSWORD')
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    ketch_version = '0.3.' + System.getenv('GITHUB_RUN_NUMBER')
}

project('app') {
    publishing {
        publications {
            sample(MavenPublication) {
                groupId 'web-sdk'
                version ketch_version
                artifactId 'websdk-android-sample'

                artifact("$buildDir/outputs/apk/debug/app-debug.apk")
            }
        }
    }
}

artifactory {
    contextUrl = 'https://ketch.jfrog.io/artifactory/'

    publish {
        repository {
            repoKey = 'android'

            username = System.getenv('ARTIFACTORY_USERNAME')
            password = System.getenv('ARTIFACTORY_PASSWORD')
        }

        defaults {
            publications('sample')
            publishBuildInfo = false   //Publish build-info to Artifactory (true by default)
            publishArtifacts = true   //Publish artifacts to Artifactory (true by default)
            publishPom = true   //Publish generated POM files to Artifactory (true by default).
            publishIvy = false
            //Publish generated Ivy descriptor files to Artifactory (true by default).
            properties = ['qa.level': 'basic', 'q.os': 'android']
        }
    }
}